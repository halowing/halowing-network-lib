plugins {
	id 'java-library'
	id 'maven-publish'
}

group = 'com.halowing.lib'
version = '0.0.2-SNAPSHOT'

java {
	toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
	withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'halowing-network-lib'
            from components.java
            versionMapping {
            	usage('java-api') {
            		fromResolutionOf('runtimeClasspath')
            	}
            	usage('java-runtime') {
            		fromResolutionResult()
            	}
            }
            pom {
            	name = 'HALOWING WEB LIBRARY.'
            	description = 'Utilities for web Application maded by halowing.'
            	url = 'https://github.com/halowing/halowing-network-lib'
            	licenses {
				    license { // 'license' 블록으로 감싸야 합니다.
				        name = 'Apache License, Version 2.0'
				        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
				    }
				}
            	developers {
            		developer {
            			id = 'halowing'
            			name = 'Seonggeun Kim'
            			email = 'sgkim@halowing.com'
            		}
            	}
            } 
        }
        
        gpr(MavenPublication) {
        	from components.java
        }
    }
    
    repositories {
   		maven {
   			name = "HalowingNetworkLibRepo"
    		url = uri("https://maven.pkg.github.com/halowing/halowing-network-lib") 
    		credentials {
    			username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
    			password = project.findProperty("gpr.key")  ?: System.getenv("GITHUB_TOKEN")
    		}
    	}
    }
}

repositories {

	mavenCentral()
	maven {
		name = "HalowingBaseLibRepo"
		url = uri("https://maven.pkg.github.com/halowing/halowing-base-lib")
		credentials {
			username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
			password = project.findProperty("gpr.key")  ?: System.getenv("GITHUB_TOKEN")
		}
	}
}

dependencies {

	// https://mvnrepository.com/artifact/commons-net/commons-net
	compileOnly 'commons-net:commons-net:3.9.0'

	compileOnly 'com.halowing.lib:halowing-base-lib:0.0.4-SNAPSHOT'
	
	// https://mvnrepository.com/artifact/javax.servlet/javax.servlet-api
	compileOnly 'javax.servlet:javax.servlet-api:4.0.1'

	// https://mvnrepository.com/artifact/org.slf4j/slf4j-api
	compileOnly 'org.slf4j:slf4j-api:2.0.7'
	
	// https://mvnrepository.com/artifact/ch.qos.logback/logback-classic
	testImplementation 'ch.qos.logback:logback-classic:1.3.11'
	
	// https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter
	testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0'
	
}

tasks.named('test') {
	useJUnitPlatform()
}